apply plugin: 'com.android.application'
apply plugin: 'ticwear'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "com.njrobot.huangyouqiang.redevicemanager"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    signingConfigs {
        def Properties localProps = new Properties()
        localProps.load(new FileInputStream(file('../local.properties')))
        def Properties keyProps = new Properties()

        // 如果读取不到'keystore.props.file'属性，就使用debug keystore

        if (localProps['keystore.props.file']) {
            keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))
        } else {
            keyProps["store"] = "../buildsystem/debug.keystore"
            keyProps["alias"] = "android"
            keyProps["storePass"] = "androiddebugkey"
            keyProps["pass"] = "android"
        }
        debug {
            storeFile file(keyProps["store"])
            keyAlias keyProps["alias"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
        release {
            // release版本使用assert确保存在该属性否则报错，避免错误打包
            assert localProps['keystore.props.file'];
            storeFile file(keyProps["store"])
            keyAlias keyProps["alias"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding{
        enabled = true
    }
}

dependencies {
    provided fileTree(include: ['*.jar'], dir: 'libs')
    compile project(path: ':domain')
    compile project(path: ':data')
    wearApp project(':wear')
    apt 'com.google.dagger:dagger-compiler:2.6'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'javax.annotation:javax.annotation-api:1.2'
    compile 'com.google.dagger:dagger:2.6'
    compile 'junit:junit:4.12'
    compile 'com.google.android.gms:play-services-wearable:9.4.0'
    provided files('libs/mobvoi-api.jar')
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha8'
    compile 'com.android.support:recyclerview-v7:24.2.1'
}
